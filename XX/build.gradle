apply plugin: 'com.android.application'

android {

    dataBinding {
        enabled = true
    }

    compileSdkVersion ANDROID_BUILD_SDK_VERSION as int
    buildToolsVersion ANDROID_BUILD_TOOLS_VERSION

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'  //指定AndroidManifest目录
            java.srcDirs = ['src']   //指定java源代码目录
            resources.srcDirs = ['src'] //指定source目录
            aidl.srcDirs = ['src']  //指定aidl目录
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']   //指定资源文件目录
            assets.srcDirs = ['assets']  //指定assets目录
        }
        instrumentTest.setRoot('tests')   //测试类型路径
        debug.setRoot('build-types/debug')  //指定发布类型路径
        release.setRoot('build-types/release')
    }

    defaultConfig {
        applicationId APPLICATION_ID
        minSdkVersion ANDROID_BUILD_MIN_SDK_VERSION as int
        targetSdkVersion ANDROID_BUILD_TARGET_SDK_VERSION as int
        versionCode VERSION_CODE as int
        versionName VERSION_NAME

        manifestPlaceholders = [
                APPLICATION_ID: "${APPLICATION_ID}",
                APP_NAME      : "@string/app_name",
                BAIDU_APPKEY  : "${BAIDU_APPKEY}"
        ]
    }
    productFlavors {
        demo {
            applicationId "${APPLICATION_ID}.demo"
            versionName "demo_${defaultConfig.versionName}"
        }

        full {
            applicationId "${APPLICATION_ID}.full"
        }
    }
    buildTypes {
        release {
            minifyEnabled false  //是否开启混淆
            applicationIdSuffix ".release"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix ".debug"  //apk后缀
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:24.2.0'
    compile project(':mainp')
}
